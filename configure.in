#
# Copyright (c) 2011 CESNET
#
# LICENSE TERMS
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in
#    the documentation and/or other materials provided with the
#    distribution.
# 3. Neither the name of the Company nor the names of its contributors
#    may be used to endorse or promote products derived from this
#    software without specific prior written permission.
#
# ALTERNATIVELY, provided that this notice is retained in full, this
# product may be distributed under the terms of the GNU General Public
# License (GPL) version 2 or later, in which case the provisions
# of the GPL apply INSTEAD OF those given above.
#
# This software is provided ``as is'', and any express or implied
# warranties, including, but not limited to, the implied warranties of
# merchantability and fitness for a particular purpose are disclaimed.
# In no event shall the company or contributors be liable for any
# direct, indirect, incidental, special, exemplary, or consequential
# damages (including, but not limited to, procurement of substitute
# goods or services; loss of use, data, or profits; or business
# interruption) however caused and on any theory of liability, whether
# in contract, strict liability, or tort (including negligence or
# otherwise) arising in any way out of the use of this software, even
# if advised of the possibility of such damage.
#
# $Id$
#

# Process this file with autoconf to produce a configure script.
AC_INIT([libnetconf], m4_esyscmd([cut -f1 ./VERSION | tr -d '\n']))

m4_include([m4/ax_pthread.m4])

AC_CANONICAL_HOST

PACKAGE_VERSION=`cut -f1 ./VERSION | tr -d '\n'`
AC_SUBST(PACKAGE_VERSION)

RELEASE=1
AC_SUBST(RELEASE)

USERNAME=`git config --get user.name`
USERMAIL=`git config --get user.email`
AC_SUBST(USERNAME)
AC_SUBST(USERMAIL)

CONFIGURE_PWD=`pwd`
AC_SUBST(CONFIGURE_PWD)

STREAMS_PATH="/usr/share/$PACKAGE_NAME/streams/"

LIBS="-lrt -lutil"

# Check for pkg-config.
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR(['pkgconfig' not found.])
fi

#AC_ARG_WITH([libxml2], AC_HELP_STRING([--with-libxml2], [specific libxml2 location]),[],[])
#AC_ARG_WITH([libssh2], AC_HELP_STRING([--with-libssh2], [specific libssh2 location]),[],[])
AC_ARG_WITH([examples], AC_HELP_STRING([--with-examples], [enable building example applications]),[SUBDIRS="./examples/client/ ./examples/server/ ./examples/tools/"],[SUBDIRS=""])

AC_ARG_ENABLE([debug],
	AC_HELP_STRING([--enable-debug],[Compile with debug options]),
	CFLAGS="-g -DDEBUG",
	CFLAGS="-O3"
)
AC_ARG_ENABLE([debug-threads],
	AC_HELP_STRING([--enable-debug-threads],[Extend debug mode for threads]),
	CPPFLAGS="$CPPFLAGS -DDEBUG_THREADS"
)

############################ Check for programs ################################

# Check for architecture, only x86 and x86_64 are supported
if test -z "$host_cpu";
then
	AC_CHECK_PROG([UNAME], [uname], [yes], [no])
	if test "$UNAME" = "no"; then
		AC_MSG_ERROR(["uname not found!"])
	fi
	build_cpu=`uname -m`
	host_cpu=$build_cpu
fi
AC_MSG_CHECKING([for host architecture])
case $host_cpu in
    i?86 )
	AC_MSG_RESULT($host_cpu is supported)
	;;
    x86_64 )
	libdir='${exec_prefix}/lib64'
	AC_MSG_RESULT($host_cpu is supported)
        ;;
    * )
        AC_MSG_WARN([unsupported architecture $host_cpu])
        ;;
esac


# Check for doxygen
AC_CHECK_PROG(DOXYGEN, doxygen, doxygen)

# Check for rpmbuild
AC_CHECK_PROG(RPMBUILD, rpmbuild, rpmbuild)

# Check for standard programs
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_INSTALL

############################ Check for libraries ###############################
export PKG_CONFIG_PATH=/usr/local/${libdir##*/}/pkgconfig:$PKG_CONFIG_PATH

### LibXML2 ###
AC_CHECK_LIB([xml2], [main], [LIBS="$LIBS -lxml2"], AC_MSG_ERROR([Missing libxml2]))
AC_CHECK_PROG([XML2_CONFIG], [xml2-config], [yes], [no])
if test "$XML2_CONFIG" = "no"; then
	AC_MSG_ERROR([Missing development package of libxml2.])
fi
CFLAGS="$CFLAGS `xml2-config --cflags`"

# libssh2
AC_CHECK_LIB([ssh2], [libssh2_version], [], AC_MSG_ERROR([Missing libssh2.]))
AC_CHECK_HEADERS([libssh2.h], [], AC_MSG_ERROR([Missing libssh2 headers.]))
PKG_CHECK_MODULES([LIBSSH2_HANDSHAKE], [libssh2 >= "1.2.8"], [LIBSSH2_SESSION_HANDSHAKE="libssh2_session_handshake"],[LIBSSH2_SESSION_HANDSHAKE="libssh2_session_startup"])
PKG_CHECK_MODULES([LIBSSH2_TIMEOUT], [libssh2 >= "1.2.9"], [LIBSSH2_SET_TIMEOUT="libssh2_session_set_timeout(session,timeout)"],[LIBSSH2_SET_TIMEOUT=""])
AC_SUBST(LIBSSH2_SESSION_HANDSHAKE)
AC_SUBST(LIBSSH2_SET_TIMEOUT)

if test -n "$SUBDIRS"; then
	### libreadline ###
	AC_CHECK_LIB([readline], [readline], [], AC_MSG_ERROR([Missing libreadline]))
	AC_CHECK_HEADERS(readline/readline.h readline/history.h, [], AC_MSG_ERROR([Missing libreadline headers.]))

	### libevent ###
	AC_CHECK_LIB([event], [event_base_dispatch], [], AC_MSG_ERROR([Missing libevent]))
	AC_CHECK_HEADERS(event2/event.h, [], AC_MSG_ERROR([Missing libevent headers.]))
fi

### pthread ###
AX_PTHREAD([
	LIBS="$PTHREAD_LIBS $LIBS"
	CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
	CC="$PTHREAD_CC"], AC_MSG_ERROR([Missing POSIX threads support.]))
#AC_CHECK_LIB([pthread], [pthread_create], [], AC_MSG_ERROR([Missing POSIX threads library.]))
#AC_CHECK_HEADERS([pthread.h], [], AC_MSG_ERROR([Missing POSIX threads headers.]))

# libdbus-1
PKG_CHECK_MODULES([DBUS], [dbus-1 >= 1.0.0])
DBUS_CONFDIR="`pkg-config --variable=sysconfdir dbus-1`/dbus-1/"
# try to detect Linux distro to distinguish dbus RPM package name
if test -f /etc/SuSE-release; then
#	SUSE, including openSUSE
	BUILDREQS="$BUILDREQS dbus-1-devel >= 1.0.0"
	REQS="$REQS dbus-1 >= 1.0.0"
elif test -f /etc/redhat-release; then
#	RedHat including RHEL, CentOS, ScientificLinux
	BUILDREQS="$BUILDREQS dbus-devel >= 1.0.0"
	REQS="$REQS dbus >= 1.0.0"
fi
# if you are using other distro, DBUS requirements are not included in spec file

CFLAGS="$CFLAGS $DBUS_CFLAGS"
LIBS="$LIBS $DBUS_LIBS"

###################### Check for configure parameters ##########################

######################### Checks for header files ##############################
AC_CHECK_HEADERS([netdb.h stddef.h stdlib.h string.h sys/socket.h termios.h unistd.h], [], AC_MSG_ERROR([Missing system headers.]))

######## Checks for typedefs, structures, and compiler characteristics #########
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_TYPE_UINT8_T

######################## Checks for library functions ##########################
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_CHECK_FUNCS([memset socket strdup strerror strstr strtoul strtoull])

############################### Set output #####################################
AC_SUBST(V_SHLIB_CMD)
AC_SUBST(V_SHLIB_OPT)
AC_SUBST(V_SONAME_OPT)
AC_SUBST(BUILDREQS)
AC_SUBST(REQS)
AC_SUBST(DISTRO)
AC_SUBST(SUBDIRS)
AC_SUBST(STREAMS_PATH)
AC_SUBST(DBUS_CONFDIR)

AC_SUBST(DOXYGEN)
if test -z "$DOXYGEN"; then
	AC_MSG_WARN([Due to missing doxygen you will not able to generate Doxygen documentation.])
fi

AC_SUBST(RPMBUILD)
if test -z "$RPMBUILD"; then
	AC_MSG_WARN([Due to missing rpmbuild you will not able to generate RPM package.])
fi

AC_CONFIG_FILES(Makefile src/config.h libnetconf.spec libnetconf.pc Doxyfile)
if test -n "$SUBDIRS"; then
	AC_CONFIG_FILES(examples/client/Makefile examples/server/Makefile examples/tools/Makefile)
fi

AC_OUTPUT
